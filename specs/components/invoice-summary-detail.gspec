@script ../my-methods.js
@script ../my-rules.js
@import ../galen-extras/galen-extras-rules.gspec

@objects
	cols-*	td
	qty 	td
	desc-name 	xpath	(.//td)[2]/div
	desc-desc 	xpath	((.//td)[2]/div)[2]
	total-amount 	xpath 	(.//td)[3]/div/span
	each-amount 	xpath 	((.//td)[3]/div)[2]

= Invoice Summary Detail Section =
	qty, desc-name, total-amount:
		css font-family contains "verta"
		#css font-size is "14px"
		#css font-weight matches "^(normal|300)$"
		#css font-style is "normal"
		#css font-stretch is "normal"

	qty:
		inside parent ~${parseFloat("4") - parseFloat(getCssProperty("qty", "padding-top"))} px top
		inside parent ~${parseFloat("4") - parseFloat(getCssProperty("qty", "padding-left"))} px left
		aligned horizontally top desc-name ~9px
		left-of desc-name >=0 px

	total-amount:
		inside parent ~4 px top
		| height ~16 px
		#css text-align is "right"
		text starts "${currencyIdentifier}"

	desc-desc, each-amount:
		css font-family contains "verta"
		#css font-size is "12px"
		#css font-weight matches "^(normal|300)$"
		#css font-style is "normal"
		#css font-stretch is "normal"

	each-amount:
		| height ~14 px
		#css text-align is "right"
		text starts "(${currencyIdentifier}"
		aligned vertically right total-amount ~2 px
		below total-amount ~2px
		#aligned horizontally top desc-desc
		right-of desc-desc >=0 px

	desc-name:
		inside parent ~4 px top
		height >=16 px
		aligned vertically left desc-desc
		above desc-desc ~2 px
		aligned horizontally top total-amount ~2 px
		left-of total-amount >=0 px

	| cols-* are aligned horizontally next to each other with equal distance