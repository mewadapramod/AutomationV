@script my-methods.js
@script my-rules.js

@objects
    client-logo  .navbar-brand-logo:first-child
    client-logo-mobile  .navbar-brand-logo:nth-child(2)
    header      .container > .navbar-header
    container   .container
    hamburger   button.navbar-toggle
    menu-items-*    nav[id*='block-loggedinmenu'] ul[class*='menu'] li a:not([style*='display: none'])
    user-full-name  xpath   .//div[@class='user-fullname']/a

@set
    laptopMargin    ~2.5% of screen/width
    desktopMargin   ~6% of screen/width
    tabletMargin    ~2.6% of screen/width
    minitabletMargin    ~1.8% of screen/width
    mobileMargin    ~1.3% of screen/width
    
= Main =
    @on tablet, desktop, laptop
        global:
            count visible menu-items-* is >= 0

    @on mini-tablet, mobile
        global:
            count visible menu-items-* is 0
   
= Header =
    @on *
        header:
            height ~55 px
            inside screen 0px top
            
= Menu-Item =
    @on *
        @forEach [menu-items-*] as menuItem, next as nextItem
            ${menuItem}:
                @if ${isVisible(menuItem)}
                    | height ~21 px
                    @if ${isVisible(nextItem)}
                        | left-of ${nextItem} ~26 px
                        aligned horizontally all ${nextItem}
                    #css font-size is "18px"
                    css font-family contains "verta"
                    #css font-style is "normal"
                    #css font-stretch is "normal"
                    #css text-align is "left"
                    #css color is "rgba(250, 250, 250, 1)" 
                    inside header ~${parseFloat("25") - parseFloat(getCssProperty(menuItem, "padding-top"))}px top

    @on laptop, desktop, laptop
        menu-items-1:
            | if "menu-items-1" is visible
                right-of client-logo >= 0 px

= User Full Name =
    @on mini-tablet, mobile
        user-full-name:
            absent

    @on laptop, desktop, tablet
        user-full-name:
            height ~54 px
            css font-family contains "verta"
            #css font-size is "18px"
            #css font-weight is "300"
            #css font-style is "normal"
            #css font-stretch is "normal"
            #css text-align is "right"
            | if "menu-items-1" is visible
                aligned horizontally centered menu-items-1 ~2px

    @on laptop
        user-full-name:
            inside screen ${laptopMargin} right

    @on desktop
        user-full-name:
            inside screen ${desktopMargin} right

    @on tablet
        user-full-name:
            inside screen ${tabletMargin} right
            
= Client-Logo =
    @on laptop, desktop, tablet, mini-tablet
        client-logo:
            height 1 to 50 px
            centered vertically inside header ~10 px
            
    @on laptop
        client-logo:
            inside screen ${laptopMargin} left

    @on desktop
        client-logo:
            inside screen ${desktopMargin} left

    @on tablet
        client-logo:
            inside screen ${tabletMargin} left

    @on mini-tablet
        client-logo:
            inside screen ~44.5% of screen/width left

    @on mobile
       client-logo-mobile:
            inside screen ~38.9% of screen/width left
            height 1 to 40 px
            centered vertically inside header ~10 px
            
= Hamburger =
    @on laptop, desktop, tablet
        hamburger:
            absent

    @on mini-tablet, mobile
        hamburger:
            | height ~32 px
            width ~50 px
            css font-family contains "verta"
            ##css font-size is "32px"
            #css font-style is "normal"
            #css font-stretch is "normal"
            #css color contains "rgba(255, 255, 255"
            inside partly header ~${parseFloat("10") - parseFloat(getCssProperty("hamburger", "padding-top"))}px top
            inside partly screen ~0px top