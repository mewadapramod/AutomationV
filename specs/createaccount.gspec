@import react-component-header.gspec
@script my-methods.js
@script my-rules.js

@objects
    passwordInput   xpath   .//input[@name='password']
    passwordVisibility  .passwordShowHide
    input-fields-*    xpath   .//form//div[@class='container-fluid']//input[@role='input']
    terms-condition-link    div[class*='container-fluid'] div[class*='midFormLink']
    mloginBox  div.text-left
    
= Create Account =
    @on *
        accountComponentTitle:
            #text is "Create Account"

= Input Fields =
    @on *
        @forEach [input-fields-*] as inputField, next as nextInputField
            ${inputField}:
                @if ${isVisible(inputField)}
                    | height ~36 px
                    @if ${isVisible(nextInputField)}
                        @if ${find(inputField).width() < 200}
                            @if ${find(inputField).width() === find(nextInputField).width()}
                                aligned horizontally all ${nextInputField} ~16px
                                @on desktop, laptop, tablet
                                    left-of ${nextInputField} ~30px
                                @on mobile, mini-tablet
                                    left-of ${nextInputField} ~10px
                                width 100% of ${nextInputField}/width
                            @else
                                aligned vertically right ${nextInputField} ~1 px
                                @on tablet
                                    above ${nextInputField} ~34 px
                                @on desktop, laptop, mini-tablet, mobile
                                    above ${nextInputField} ~44 px
                        @else
                            @on tablet
                                above ${nextInputField} ~34 px
                            @on desktop, laptop, mini-tablet
                                above ${nextInputField} ~44 px
                            @on desktop, laptop, mini-tablet
                            	width 100% of ${nextInputField}/width
                            	aligned vertically left accountComponentTitle

                    css font-family contains "verta"
                    #css font-size is "16px"
                    #css font-style is "normal"
                    #css font-stretch is "normal"
                    #css line-height is "24px"
                    #css text-align is "start"
                    #css color contains "rgba(68, 68, 68"

        input-fields-1:            
            aligned vertically left accountComponentTitle
            @on tablet
                below accountComponentTitle ~10 px

            @on mini-tablet, mobile
                below accountComponentTitle ~17 px

            @on desktop, laptop
                below accountComponentTitle ~17 px

= Password =
    @on *
        | if "passwordVisibility" is visible
            passwordVisibility:
                visible
                inside partly passwordInput 0px right

    @on tablet
        passwordInput:
            inside loginBox ~20px right

    @on desktop, laptop
        passwordInput:
            inside loginBox ~30px right

    @on mini-tablet, mobile
        passwordInput:
            inside loginBox ~25px right

= Terms & Conditions =
    @on *
        terms-condition-link:
            visible

    | if "terms-condition-link" is visible
        @on *
            terms-condition-link:
                #text is "By creating an account, you agree to our Terms and Conditions."
                aligned vertically all passwordInput 10 px
                height ~28px
                css font-family contains "verta"
                #css font-size is "12px"
                #css font-style is "normal"
                #css font-stretch is "normal"
                #css line-height is "17px"
                #css color contains "rgba(68, 68, 68"
                aligned vertically left accountComponentTitle 10 px

        @on tablet
            terms-condition-link:
                above submitBtn ~9px
                inside loginBox ~20px right

        @on desktop, laptop
            terms-condition-link:
                above submitBtn ~14px
                inside loginBox ~30px right

        @on mini-tablet, mobile
            terms-condition-link:
                above submitBtn ~14px
                inside loginBox ~30px right

= Create Account =
    @on *
        submitBtn:
            aligned vertically all passwordInput 20 px
            #text is "SIGN UP"

        redirectionLink:
            #text is "Already have an account? Click here"

    @on tablet
        submitBtn:
            | if "terms-condition-link" is visible
                below terms-condition-link ~9px

    @on desktop, laptop, mini-tablet, mobile
        submitBtn:
            | if "terms-condition-link" is visible
                below terms-condition-link ~17px

= Email =
    @on mini-tablet, mobile
        emailInput:
            inside mloginBox ~5px right

= Login Box =
    @on mobile
        mloginBox:
            #below header ~${parseFloat("10") - parseFloat(getCssProperty("loginBox", "padding-top")) - parseFloat(getCssProperty("header", "padding-bottom"))} px
            #inside screen ${mobileMargin} left right
            # inside screen

= Account Component Title =
    @on mini-tablet, mobile
        accountComponentTitle:
            inside mloginBox ~5px right                                